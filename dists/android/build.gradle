buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

dependencies {
    repositories {
        google()
        mavenCentral()
    }
}

// Enable to see use of deprecated API
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

//gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS_FULL

apply plugin: 'com.android.application'

android {
    compileSdk 33
    buildToolsVersion "33.0.1"
    ndkVersion "21.3.6528147"

    namespace "org.scummvm.scummvm"

    defaultConfig {
        applicationId "org.scummvm.scummvm"

        setProperty("archivesBaseName", "ScummVM")

        minSdkVersion 16
        targetSdkVersion 33

        versionName "2.8.0"
        versionCode 113
        // versioncode must be incremented for each market upload
        // ScummVM 2.8.0:   113 - 116 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.7.x:   109 - 112 (reserve for potential beta or bug fix release)
        // Historical version codes:
        // ScummVM 2.7.0.5: 105 - 108 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (proper release on Play Store)
        // ScummVM 2.7.0.4: 101 - 104 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 4 on Play Store)
        // ScummVM 2.7.0.2:  97 - 100 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 3 on Play Store)
        // ScummVM 2.7.0.1:  93 -  96 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 2 on Play Store)
        // ScummVM 2.7.0:    89 -  92 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 1 on Play Store)
        // ScummVM 2.6.x:    85 -  88 (skipped - was reserved for potential beta or 2.6.x bugfix builds)
        // ScummVM 2.6.x:    81 -  84 (skipped - was reserved for potential beta or 2.6.x bugfix builds)
        // ScummVM 2.6.1:    77 -  80 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.6.0:    73 -  76 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.5.1:    69 -  72 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.5.0:    65 -  68 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    61 -  64 Play Store release version for 2.2.1 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    57 -  60 Beta 3 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    53 -  56 Beta 2 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    49 -  52 Beta 1 (29 Sep 2020) (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.0:    45 -  48 (arm-v7a, arm64-v8a, x86, x86_64 respectively -- armeabi was dropped)
        // ScummVM 2.1.1:    40 -  44 (armeabi, arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.1.0:    35 -  39 (armeabi, arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.0.0:    30 -  34
        // ScummVM 1.9.0.1:  25 -  28
        // ScummVM 1.9.0:    19
        // ScummVM 1.8.1:    15

    }
    buildTypes {
        debug{
            debuggable true
        }
        release {
            debuggable false
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled false

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            //shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
    sourceSets {
        main {
            assets.srcDirs 'assets/'
            java.srcDirs srcdir + '/backends/platform/android/'
            jniLibs.srcDirs 'lib/'
            res.srcDirs srcdir + '/dists/android/res/'
            manifest.srcFile srcdir + '/dists/android/AndroidManifest.xml'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.5.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
}
