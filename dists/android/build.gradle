buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.1'
    }
}

dependencies {
    repositories {
        google()
        mavenCentral()
    }
}

// Load our source dependent properties
def srcProperties = new Properties()
srcProperties.load(new FileInputStream(rootProject.file("src.properties")))

// Enable to see use of deprecated API
tasks.withType(JavaCompile).configureEach {
	options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

//gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS_FULL

apply plugin: 'com.android.application'

android {
    compileSdk 34
    ndkVersion "23.2.8568313"

    namespace "org.scummvm.scummvm"

    defaultConfig {
        applicationId "org.scummvm.scummvm"

        base.archivesName = "ScummVM"

        minSdkVersion 16
        targetSdkVersion 34

        versionName "2.9.0"
        versionCode 2090001
        // versioncode must be incremented for each market upload
		// maximum versioncode allowed by Play Store is: 2100000000
		// After 2.8.0 release the versioncode numbering schema changed to be more immediately helpful.
		// It now follows the format: M.mm.p[.bb][.a]
		// (M is major release version number)
		// (m is minor release version number)
		// (p is patch/bugfix release number)
		// (b is build number, eg. re-release or beta testing was done on Play Store)
		// (a is a number indicating the target architecture (ABI)):
		//     (1: arm-v7a, 2: arm64-v8a, 3: x86, 4: x86_64)
		// eg. ScummVM 2.9.0 builds would have version codes: 2090001 - 2090004
		// --------------
        // ScummVM 2.8.0:   113 - 116 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.7.1:   109 - 112 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (release on Play Store)
        // Historical version codes:
        // ScummVM 2.7.0.5: 105 - 108 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (proper release on Play Store)
        // ScummVM 2.7.0.4: 101 - 104 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 4 on Play Store)
        // ScummVM 2.7.0.2:  97 - 100 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 3 on Play Store)
        // ScummVM 2.7.0.1:  93 -  96 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 2 on Play Store)
        // ScummVM 2.7.0:    89 -  92 (arm-v7a, arm64-v8a, x86, x86_64 respectively) (beta 1 on Play Store)
        // ScummVM 2.6.x:    85 -  88 (skipped - was reserved for potential beta or 2.6.x bugfix builds)
        // ScummVM 2.6.x:    81 -  84 (skipped - was reserved for potential beta or 2.6.x bugfix builds)
        // ScummVM 2.6.1:    77 -  80 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.6.0:    73 -  76 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.5.1:    69 -  72 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.5.0:    65 -  68 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    61 -  64 Play Store release version for 2.2.1 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    57 -  60 Beta 3 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    53 -  56 Beta 2 (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.1:    49 -  52 Beta 1 (29 Sep 2020) (arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.2.0:    45 -  48 (arm-v7a, arm64-v8a, x86, x86_64 respectively -- armeabi was dropped)
        // ScummVM 2.1.1:    40 -  44 (armeabi, arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.1.0:    35 -  39 (armeabi, arm-v7a, arm64-v8a, x86, x86_64 respectively)
        // ScummVM 2.0.0:    30 -  34
        // ScummVM 1.9.0.1:  25 -  28
        // ScummVM 1.9.0:    19
        // ScummVM 1.8.1:    15

    }
    buildTypes {
        debug{
            applicationIdSuffix ".debug"
            manifestPlaceholders = [nameSuffix:"_debug"]
            debuggable true
        }
        release {
            manifestPlaceholders = [nameSuffix:""]
            debuggable false
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled false

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            //shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            assets.srcDirs 'assets/'
            java.srcDirs srcProperties['srcdir'] + '/backends/platform/android/'
            jniLibs.srcDirs 'lib/'
            res.srcDirs srcProperties['srcdir'] + '/dists/android/res/'
            manifest.srcFile srcProperties['srcdir'] + '/dists/android/AndroidManifest.xml'
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.7.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
}
