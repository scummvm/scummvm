ON ERROR PROCerror
SYS "XOS_ReadVarVal","StrongHelp$Dir",-1,-1 TO ,,stronghelp%
file$=""
readme$=""

SYS "Territory_Number" TO current_territory%
REPEAT
   READ territory%, prefix$, readme$
   WHILE readme$<>""
      IF territory%=current_territory% OR territory%=-1 THEN
         SYS "OS_File",20,"<ScummVM$Dir>.docs."+prefix$+readme$ TO type%,,,,,,ftype%
         IF type%<>0 AND NOT (stronghelp%=0 AND ftype%=&3D6) THEN
            file$="<ScummVM$Dir>.docs."+prefix$+readme$
         ENDIF
      ENDIF
      READ readme$
   ENDWHILE
UNTIL territory%=-1 OR file$<>""

IF file$<>"" THEN
   OSCLI "Filer_Run "+file$
ENDIF
END

DEF PROCerror
   ON ERROR OFF
   ERROR ERR, REPORT$+" at line "+STR$(ERL)
ENDPROC

REM Reference: https://www.riscosopen.org/wiki/documentation/show/Territory%20Numbers
DATA 4,   "it.",    "GuidaRapida",                                         ""
DATA 5,   "es.",    "InicioRapido",                                        ""
DATA 6,   "fr.",    "DemarrageRapide",                                     ""
DATA 7,   "de.",    "Schnellstart",                 "LIESMICH",            ""
DATA 11,  "se.",    "Snabbstart",                   "LasMig",              ""
DATA 14,  "da.",    "HurtigStart",                                         ""
DATA 15,  "no-nb.", "HurtigStart",                                         ""
DATA 134, "cz.",                                    "PrectiMe",            ""
DATA -1,  "",       "QuickStart",      "README/md", "README",   "ScummVM", ""
