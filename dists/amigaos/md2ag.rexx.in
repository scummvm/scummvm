/*
$VER: md2ag.rexx 0.1 (23.05.2021) README(.md) to (amiga).guide converter.
Converts a given markdown README file to a basic hypertext Amiga guide file
and installs it to a given location.
*/

PARSE ARG p_readme p_instpath

/*
Check if arguments are available, otherwise quit.
*/
IF ~ARG() THEN DO
	SAY 'No Arguments given!'
	SAY 'Usage: md2ag.rexx README.md INSTALLPATH'
	EXIT
END

/*
If the given filename/path has spaces in it, AmigaDOS/CLI
will add extra quotation marks to secure a sane working path.
Get rid of them to make AREXX find the file and remove leading
and trailing spaces.
*/
IF ~EXISTS(p_readme) THEN DO
	SAY p_readme' not available!'
	EXIT
END
ELSE DO
	p_readme=STRIP(p_readme)
	p_readme=COMPRESS(p_readme,'"')
END
IF p_instpath='' THEN DO
	SAY 'No installation destination given!'
	EXIT
END
ELSE DO
	p_instpath=STRIP(p_instpath)
	p_instpath=STRIP(p_instpath,'T','/')
	p_instpath=COMPRESS(p_instpath,'"')
END

OPEN(read_md,p_readme,'R')

IF READCH(read_md,18)~='# [ScummVM README]' THEN DO
	CLOSE(read_md)
	SAY p_readme' is not the ScummVM README.md file. Aborting!'
	EXIT
END
ELSE
	/*
	Skip the first "Build Status" line.
	*/
	CALL READLN(read_md)

OPEN(write_guide,'README.guide','W')

/*
Prepare Amiga guide, add intro and fixed text.
*/
CALL WRITELN write_guide,'@DATABASE ScummVM README.guide'
CALL WRITELN write_guide,'@$VER: ScummVM Readme @VERSION@'
CALL WRITELN write_guide,'@(C) by The ScummVM team'
CALL WRITELN write_guide,'@AUTHOR The ScummVM team'
CALL WRITELN write_guide,'@WORDWRAP'
CALL WRITELN write_guide,'@NODE "main" "ScummVM README Guide"'
CALL WRITELN write_guide,'@{b}'
CALL WRITELN write_guide,ScummVM README
CALL WRITELN write_guide,'@{ub}'

DO WHILE ~EOF(read_md)
	v_line=READLN(read_md)

	/*
	Check the one "rolled over" line and rejoin it before progressing.
	It's lines 11, 12 and 13 in the opriginal .md, but we only join 12 and 13
	due to the cut weblink:
	latest release, progress reports and more, please visit the ScummVM [home
	page](https://www.scummvm.org/).
	*/
	IF POS('[',v_line)>0 THEN DO
		IF POS(']',v_line)=0 THEN DO
			rejoin_line=READLN(read_md)
			v_line=v_line''rejoin_line
		END
	END

	/*
	Replace any  '�' characters with '`'.
	*/
	DO WHILE POS('�',v_line)>0
		v_line=OVERLAY('`',v_line,POS('�',v_line),1)
	END

	/*
	Change local markdown links to AmigaGuide ones.
	*/
	IF POS('[here](',v_line)>0 THEN DO
		v_locallink=SUBSTR(v_line,POS('(',v_line)+1,POS(')',v_line)-POS('(',v_line)-1)
		v_line=DELSTR(v_line,POS('(',v_line),POS(')',v_line)-POS('(',v_line))
		v_line=INSERT('@{"'v_locallink'" link 'v_locallink'/main}',v_line,POS(']',v_line))
	END

	/*
	Change html markdown links to AmigaGuide ones.
	*/
	IF POS('http',v_line)>0 THEN DO
		IF POS('(http',v_line)>0 THEN DO
			v_weblink=SUBSTR(v_line,POS('(',v_line)+1,POS(')',v_line)-POS('(',v_line)-1)
			v_line=DELSTR(v_line,POS('(',v_line),POS(')',v_line)-POS('(',v_line))
			v_line=INSERT('@{"'v_weblink'" System "URLOpen 'v_weblink'"}',v_line,POS(']',v_line))
		END
		ELSE DO
			v_weblink=SUBSTR(v_line,POS('<',v_line)+1,POS('/>',v_line)-POS('<',v_line))
			v_line=DELSTR(v_line,POS('<',v_line)+1,POS('/>',v_line)-POS('<',v_line))
			v_line=INSERT('@{"'v_weblink'" System "URLOpen 'v_weblink'"}',v_line,POS('>',v_line)+2)
		END
	END

	CALL WRITELN write_guide,v_line
END

/*
Close guide and clean up.
*/
CALL WRITELN write_guide,'@ENDNODE'

CLOSE(read_md)
CLOSE(write_guide)

/*
Install finished README.guide to installation path and delete README.guide.
*/
ADDRESS COMMAND 'copy README.guide 'p_instpath
ADDRESS COMMAND 'delete README.guide'

EXIT
