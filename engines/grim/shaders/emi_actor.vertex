in vec3 position;
in vec2 texcoord;
in vec4 color;

uniform highp mat4 modelMatrix;
uniform highp mat4 viewMatrix;
uniform highp mat4 projMatrix;
uniform highp mat4 extraMatrix;
uniform highp vec3 cameraPos;
uniform highp vec3 actorPos;
uniform bool textured;
uniform bool isBillboard;

out vec2 Texcoord;
out vec4 Color;

void main()
{
	vec4 pos = vec4(position, 1.0);
	if (isBillboard) {
		vec4 scaled = extraMatrix * pos;
		vec3 camToPos = normalize(actorPos - cameraPos);
		vec3 up = vec3(0.0, 1.0, 0.0);
		vec3 right = cross(camToPos, up);
		pos.xyz = scaled.x * right + scaled.y * up;
		pos = modelMatrix * pos;
	} else {
		pos = modelMatrix * extraMatrix * pos;
	}
	pos /= pos.w;
	pos = viewMatrix * pos;
	pos += viewMatrix * vec4(actorPos - cameraPos, 0.0);
	pos.z *= -1.0;
	
	gl_Position = projMatrix * pos;
	if (isBillboard)
		gl_Position.z = round(gl_Position.z);
	
	Color = color;
	if (textured) {
		Texcoord = texcoord;
		if (isBillboard)
			Texcoord.x = 1.0 - Texcoord.x;
	} else {
		Texcoord = vec2(0.0, 0.0);
	}
}
